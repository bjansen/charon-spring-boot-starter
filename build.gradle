plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.18.16'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
}

subprojects {

    apply plugin: 'java-library'
    apply plugin: 'groovy'
    apply plugin: 'signing'
    apply plugin: 'maven-publish'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    repositories {
        mavenCentral()
    }

    ext {
        springBootVersion = '4.0.0'
        resilience4jVersion = '2.3.0'
        httpClient5Version = '5.5.1'

        group = 'com.github.mkopylec'
        pomContent = {
            name = 'Charon Spring Boot Starter'
            description = 'Reverse proxy implementation in form of Spring Boot starter'
            url = 'https://github.com/mkopylec/charon-spring-boot-starter'
            licenses {
                license {
                    name = 'The Apache License, Version 2.0'
                    url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
            developers {
                developer {
                    id = 'mkopylec'
                    name = 'Mariusz Kopylec'
                    email = 'mariusz.kopylec@o2.pl'
                }
            }
            scm {
                connection = 'scm:git:https://github.com/mkopylec/charon-spring-boot-starter.git'
                developerConnection = 'scm:git:https://github.com/mkopylec/charon-spring-boot-starter.git'
                url = 'https://github.com/mkopylec/charon-spring-boot-starter'
            }
        }
    }

    scmVersion {
        tag {
            prefix = ''
        }
    }

    group = project.ext.group
    base {
        archivesName = name
    }
    version = scmVersion.version

    dependencies {
        api group: 'org.springframework', name: 'spring-web', version: '7.0.1'
        api group: 'io.github.resilience4j', name: 'resilience4j-circuitbreaker', version: resilience4jVersion
        api group: 'io.github.resilience4j', name: 'resilience4j-ratelimiter', version: resilience4jVersion
        api group: 'io.github.resilience4j', name: 'resilience4j-retry', version: resilience4jVersion
        api group: 'io.github.resilience4j', name: 'resilience4j-micrometer', version: resilience4jVersion
        api group: 'io.micrometer', name: 'micrometer-core', version: '1.16.0'
        api group: 'org.apache.commons', name: 'commons-lang3', version: '3.20.0'
        api group: 'org.apache.commons', name: 'commons-collections4', version: '4.5.0'
        api group: 'commons-io', name: 'commons-io', version: '2.21.0'

        testImplementation 'org.junit.jupiter:junit-jupiter:6.0.1'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    test {
        useJUnitPlatform()
    }
}

nexusPublishing {
    packageGroup = 'com.github.mkopylec'
    repositories {
        sonatype {
            username = project.ext.has('ossrhUsername') ? project.ext.ossrhUsername : ''
            password = project.ext.has('ossrhPassword') ? project.ext.ossrhPassword : ''
        }
    }
}

wrapper {
    gradleVersion = '9.0.0'
}
